// app config
apply plugin: 'com.android.application'

def camera_folder = 'Camera2'

def srcFolder = [
        camera_folder + '/src',
        camera_folder + '/src_pd',
        camera_folder + '/src_pd_gcam',
        camera_folder + '/config/src',
]

def resFolder = [
        camera_folder + '/res',
        camera_folder + '/res_p',
]

def MANIFEST_REPLACE = false
def manifestFile = camera_folder + '/AndroidManifest.xml'
def manifestFileReplace = camera_folder + '/src_plus/AndroidManifest.xml'

android {
    def compileSdkVersionInt = 25
    compileSdkVersion compileSdkVersionInt
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.baby518.camera2"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode getCurrentDate() as int
        versionName "1.0.3." + getCurrentDate()
    }

    productFlavors {
        AOSP_ORIG_ {
            buildConfigField("boolean", "USE_API2", "true")
            buildConfigField("boolean", "USE_FILMSTRIP", "true")
            buildConfigField("boolean", "SUPPORT_NAVIGATION_BAR", "false")
            buildConfigField("boolean", "FORCE_PORTRAIT_SCREEN", "false")
//            MANIFEST_REPLACE = false
        }
        PLUS_ {
            MANIFEST_REPLACE = true
            def FORCE_PORTRAIT_SCREEN = true
            buildConfigField("boolean", "USE_API2", "false")
            buildConfigField("boolean", "USE_FILMSTRIP", "false")
            buildConfigField("boolean", "SUPPORT_NAVIGATION_BAR", "true")
            buildConfigField("boolean", "FORCE_PORTRAIT_SCREEN", "${FORCE_PORTRAIT_SCREEN}")
            manifestPlaceholders = [SCREEN_ORIENTATION: FORCE_PORTRAIT_SCREEN ? 'portrait' : 'unspecified']
        }
    }

    sourceSets {
        main {
            if (MANIFEST_REPLACE) {
                manifest.srcFile file(manifestFileReplace)
            } else {
                manifest.srcFile file(manifestFile)
            }
            def assetsFolder = [camera_folder + '/assets']
            def libsFolder = [camera_folder + '/libs']

            java.srcDirs = srcFolder
            resources.srcDirs = srcFolder
            aidl.srcDirs = srcFolder
            renderscript.srcDirs = srcFolder
            res.srcDirs = resFolder
            assets.srcDirs = assetsFolder
//            jni.srcDirs = ['jni']
            jniLibs.srcDirs = libsFolder
        }

        PLUS_ {
            java.srcDirs = [camera_folder + '/src_plus']
            res.srcDirs = [camera_folder + '/res_plus'] + [camera_folder + '/overlay_res']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    if (compileSdkVersionInt >= 23) {
        useLibrary 'org.apache.http.legacy'
    }
}

dependencies {
    provided files(getLayoutLibPath(android))
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':framework_ex')
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.guava:guava:19.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.adobe.xmp:xmpcore:5.1.2'
}

def static getCurrentDate() {
    return new Date().format('yyyyMMddHH')
}

// must called after "android" definition
def static getLayoutLibPath(Object android) {
    def rootDir = "${android.getSdkDirectory().getAbsolutePath()}" + "/platforms/" + android.compileSdkVersion + "/data/layoutlib.jar"
    Console.println(rootDir)
    return rootDir
}
